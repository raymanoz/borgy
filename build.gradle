plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'idea'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile 'org.http4k:http4k-core:3.163.0'
    compile 'org.http4k:http4k-server-jetty:3.163.0'
    compile 'org.http4k:http4k-cloudnative:3.163.0'
    compile 'org.http4k:http4k-client-okhttp:3.163.0'
    compile 'org.http4k:http4k-format-gson:3.163.0'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def webAppFiles = fileTree(dir: project.file('src/main/react/build')).include('**/*')

task uiClean(type: Exec) {
    workingDir 'src/main/react'
    commandLine(['yarn', 'clean'])
}

task uiBuild(type: Exec) {
    workingDir 'src/main/react'
    commandLine(['yarn', 'build'])
}

jar {
    manifest {
        attributes 'Main-Class': 'com.raymanoz.MainKt'
    }
    from(webAppFiles) {
        into 'client'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

jar.dependsOn(['uiBuild'])

clean.dependsOn(['uiClean'])

