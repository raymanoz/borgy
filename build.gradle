plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'idea'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    def http4k = '3.172.0'
    compile "org.http4k:http4k-core:$http4k"
    compile "org.http4k:http4k-cloudnative:$http4k"
    compile "org.http4k:http4k-client-okhttp:$http4k"
    compile "org.http4k:http4k-format-jackson:$http4k"
    compile "com.natpryce:result4k:2.0.0"
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testImplementation "org.http4k:http4k-testing-hamkrest:$http4k"
    testImplementation 'com.natpryce:hamkrest:1.7.0.0'
}

idea {
    project {
        jdkName "11"
        languageLevel "11"
        vcs = 'Git'
    }
    module {
        outputDir file("${buildDir}/classes/kotlin/main")
        testOutputDir file("${buildDir}/classes/kotlin/test")
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

def webAppFiles = fileTree(dir: project.file('src/main/react/build')).include('**/*')

task pull_submodules(type: Exec) {
    commandLine(['git', 'submodule', 'update', '--init', '--recursive'])
}

task init_borgy(type: Exec, dependsOn: ['pull_submodules']) {
    workingDir 'src/main/react'
    commandLine(['yarn', 'install'])
}

task ui_clean(type: Exec) {
    workingDir 'src/main/react'
    commandLine(['yarn', 'clean'])
}

task ui_build(type: Exec) {
    workingDir 'src/main/react'
    commandLine(['yarn', 'build'])
}

task start_back(type: JavaExec) {
    main = 'com.raymanoz.borgy.MainKt'
    systemProperty("config", "dev")
    classpath = sourceSets.main.runtimeClasspath
}

task start_front(type: Exec) {
    workingDir 'src/main/react'
    commandLine(['yarn', 'start'])
}

/*
task start_jar(type: JavaExec, dependsOn: jar) {
    classpath = "build/libs/borgy.jar"
}
*/

jar {
    manifest {
        attributes 'Main-Class': 'com.raymanoz.borgy.MainKt'
    }
    from(webAppFiles) {
        into 'public'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

jar.dependsOn(['ui_build'])

clean.dependsOn(['ui_clean'])


